AC_PREREQ(2.59)
AC_INIT([GNOME Nettool], [2.16.0], [http://bugzilla.gnome.org/enter_bug.cgi?product=gnome-nettool], [gnome-nettool])
AC_CONFIG_SRCDIR([src/ping.c])

dnl Useful to call aclocal after 'make'
AC_SUBST(ACLOCAL_AMFLAGS, "$ACLOCAL_FLAGS")

AC_CONFIG_HEADERS([config.h])
AM_INIT_AUTOMAKE([1.9 foreign])
AC_CONFIG_MACRO_DIR([m4])
AM_MAINTAINER_MODE

IT_PROG_INTLTOOL([0.35.0])

AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC([])
AC_CHECK_HEADERS(sys/sockio.h)
AC_CHECK_HEADERS(sys/socket.h ifaddrs.h,,AC_MSG_ERROR(Required header not found: $ac_header))
dnl AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS(yes)
CFLAGS="$CFLAGS $WARN_CFLAGS"

dnl ******************************************
dnl ** pkg-config dependacy checks          **
dnl ******************************************


GTK_REQUIRED=2.5.4
LIBGLADE_REQUIRED=2.0.0
GCONF_REQUIRED=1.1.11

PKG_CHECK_MODULES(NETTOOL, 
		  gtk+-2.0       >= $GTK_REQUIRED 
		  libglade-2.0   >= $LIBGLADE_REQUIRED 
		  gconf-2.0      >= $GCONF_REQUIRED)
AC_SUBST(NETTOOL_CFLAGS)
AC_SUBST(NETTOOL_LIBS)

dnl
dnl Translations
dnl
GETTEXT_PACKAGE=gnome-nettool
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The gettext package])
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_DOC_INIT

dnl
dnl Debugging flags
dnl
set_debug=no
AC_ARG_ENABLE(debug,
[  --enable-debug       Lots of noisy debug messages],
set_debug="$enableval")

AC_MSG_CHECKING(if debug messages are enabled)
if test "$set_debug" != "no"; then
        AC_MSG_RESULT(yes)
        CFLAGS="-DGRD_DEBUG $CFLAGS"
else
        AC_MSG_RESULT(no)
fi

AC_CONFIG_FILES([
Makefile
po/Makefile.in
pixmaps/Makefile
help/Makefile
src/Makefile
src/gnome-nettool.desktop.in])
AC_OUTPUT
