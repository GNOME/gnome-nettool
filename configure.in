AC_PREREQ(2.52)
AC_INIT(src/ping.c)

dnl Useful to call aclocal after 'make'
AC_SUBST(ACLOCAL_AMFLAGS, "$ACLOCAL_FLAGS")

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-nettool, 0.99.2)
AM_MAINTAINER_MODE

AC_PROG_INTLTOOL([0.11])

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_CHECK_HEADERS(sys/sockio.h sys/socket.h)
dnl AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS(yes)
CFLAGS="$CFLAGS $WARN_CFLAGS"

dnl ******************************************
dnl ** pkg-config dependacy checks          **
dnl ******************************************

LIBGNOMEUI_REQUIRED=2.0.0
LIBGNOME_REQUIRED=2.0.0
GTK_REQUIRED=2.4.0
LIBGLADE_REQUIRED=2.0.0
GCONF_REQUIRED=1.1.11

PKG_CHECK_MODULES(NETTOOL, 
		  libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED 
		  libgnome-2.0   >= $LIBGNOME_REQUIRED 
		  gtk+-2.0       >= $GTK_REQUIRED 
		  libglade-2.0   >= $LIBGLADE_REQUIRED 
		  gconf-2.0      >= $GCONF_REQUIRED)
AC_SUBST(NETTOOL_CFLAGS)
AC_SUBST(NETTOOL_LIBS)

dnl Find ifconfig
dnl
AC_ARG_WITH(ifconfig, [  --with-ifconfig=PATH    Where ifconfig is.])
if test "x$with_ifconfig" = "x" ; then
	AC_PATH_PROG(IFCONFIG_PROGRAM, 
		ifconfig,,
		$PATH:/sbin:/usr/etc:/usr/sbin)
	if test "x$IFCONFIG_PROGRAM" != "x" ; then
		AC_DEFINE_UNQUOTED(IFCONFIG_PROGRAM, "$IFCONFIG_PROGRAM", [The ifconfig program])
	fi
else
	AC_DEFINE_UNQUOTED(IFCONFIG_PROGRAM, "$with_ifconfig", [The ifconfig program])
fi
AM_CONDITIONAL(HAVE_IFCONFIG, test "x$IFCONFIG_PROGRAM" != "x")

dnl
dnl Translations
dnl
GETTEXT_PACKAGE=gnome-nettool
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The gettext package])
ALL_LINGUAS="az bn ca cs da de el en_CA en_GB es fr ga gl gu he hu it ja ko ms nl no pa pl pt pt_BR ru sq sr sr@Latn sv tr uk vi wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl Debugging flags
dnl
set_debug=no
AC_ARG_ENABLE(debug,
[  --enable-debug       Lots of noisy debug messages],
set_debug="$enableval")

AC_MSG_CHECKING(if debug messages are enabled)
if test "$set_debug" != "no"; then
        AC_MSG_RESULT(yes)
        CFLAGS="-DGRD_DEBUG $CFLAGS"
else
        AC_MSG_RESULT(no)
fi

AC_OUTPUT([
Makefile
po/Makefile.in
pixmaps/Makefile
src/Makefile])
